buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def katgProperties = file( "KeithAndTheGirlApp.properties" );

android {

    Properties props = new Properties()
    if( katgProperties.exists() ) {

        props.load( new FileInputStream( katgProperties ) )

    }

    signingConfigs {
        release {
            if( katgProperties.exists() ) {
                keyAlias props['keyalias']
                keyPassword props['keyalias.password']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
        }
    }

    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 5
        versionName '1.0'

        buildConfigField 'boolean', 'SEND_CRASHLYTICS', 'true'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    buildTypes {
        release {
            buildConfigField 'boolean', 'SEND_CRASHLYTICS', 'true'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField 'boolean', 'SEND_CRASHLYTICS', 'false'
            minifyEnabled false
        }
        beta {
            minifyEnabled false
            applicationIdSuffix '.beta'
//            if( katgProperties.exists() ) {
                signingConfig signingConfigs.release
//            }
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:support-annotations:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:3.1.0'

    compile 'joda-time:joda-time:2.8.1'
    compile 'commons-io:commons-io:2.4'
//    compile project(':exoplayer')

    // Crashlytics Kit
    compile('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true
    }

    // Twitter Kit
    compile('com.twitter.sdk.android:twitter:1.6.0@aar') {
        transitive = true
    }

    // Digits Kit
    compile('com.digits.sdk.android:digits:1.6.1@aar') {
            transitive = true
    }

    // MoPub Kit
    compile('com.mopub.sdk.android:mopub:3.8.0@aar') {
        transitive = true
    }

}
